AC_INIT(liblogger, 1.0.0, james@stev.org)
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_SRCDIR([src/LogManager.cpp])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_FILES([liblogger.pc])

LT_INIT
AM_INIT_AUTOMAKE([foreign])
AM_SILENT_RULES([yes])

AC_PROG_CXX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

AC_CHECK_HEADERS(syslog.h)
AC_CHECK_HEADERS([arpa/inet.h])
AC_CHECK_HEADERS([netinet/in.h])
AC_CHECK_HEADERS([sys/socket.h])
AC_CHECK_HEADERS([sys/eventfd.h])
AC_CHECK_HEADER_STDBOOL()

AC_TYPE_SSIZE_T
AC_TYPE_SIZE_T
AC_TYPE_UINT64_T

AC_CHECK_FUNCS([memset])
AC_CHECK_FUNCS([socket])
AC_CHECK_FUNCS([strerror])
AC_CHECK_FUNCS([localtime_r])

AX_CXX_COMPILE_STDCXX_11(noext, optional)

m4_ifdef([AX_COMPILER_FLAGS],
	[
		AX_COMPILER_FLAGS([WARN_CFLAGS],[WARN_LDFLAGS])
		AC_SUBST([AM_CXXFLAGS], $WARN_CXXFLAGS)
	]
,
	[
		AC_SUBST([AM_CXXFLAGS], ['-Wall -Werror'])
	]
)

m4_ifdef([AX_CODE_COVERAGE],
	[
		AX_CODE_COVERAGE
		if test "$CODE_COVERAGE_ENABLED" == "yes" ; then
			AC_SUBST([AM_CFLAGS], $CODE_COVERAGE_CFLAGS)
			AC_SUBST([AM_CPPFLAGS], $CODE_COVERAGE_CPPFLAGS)
			AC_SUBST([AM_CXXFLAGS], $CODE_COVERAGE_CXXFLAGS)
			AC_SUBST([AM_LDFLAGS], $CODE_COVERAGE_LDFLAGS)
			AC_SUBST([AM_CXXFLAGS], ['-O0 -g -fprofile-arcs -ftest-coverage'])
		fi
	]
,
	[
	]
)

AC_OUTPUT(Makefile src/Makefile tests/Makefile)

